#!/usr/bin/env perl
#
# KeyNanny provides a framework to protect sensitive data on a Unix host.
# keynanny implements a command line client for
# - setting values
# - getting values
# - listing available keys
# - rendering templates
#
# Copyright (c) 2014 The CertNanny Project
#
# Licensed under the Apache License, Version 2.0 and the GNU General Public License, Version 2.0.
# See the LICENSE file for details.
#


use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;
use Template;

use Data::Dumper;
use KeyNanny;

my %config;

sub usage {
    pod2usage({
        -exitval => 1,
        -verbose => 0,
    });
}

sub process_template {
    my $kn        = shift;
    my $infile    = shift;
    my $outfile   = shift;
    my $vars      = shift;

    my $fh;
    open( $fh, '<', $infile ) or die "Error opening template file: $!\n";
    my $input = do {
	local $/;
	<$fh>;
    };
    close $fh;

    my $template = Template->new( {} );

    if (! $template->process( \$input, $vars, $outfile )) {
	print STDERR "ERROR: Could not process template $infile: " . $template->error() . "\n";
	return;
    }

    return 1;
}

sub error_msg {
    my $result = shift;
    my $arg = shift;

    if (defined $arg) {
	print STDERR $arg . ' ';
    }
    return unless defined $result;
    return unless (ref $result eq 'HASH');
    return unless (defined $result->{STATUS});
    return 1 if ($result->{STATUS} eq 'OK');

    my $msg = '(' . $result->{STATUS};
    if (defined $result->{MESSAGE}) {
	$msg .= ': ' . $result->{MESSAGE};
    }
    $msg .= ')';

    print STDERR $msg . "\n";
    return 1;
}

my $socketfile;
my %defines;
my $outfile;
my $help;
my $man;

GetOptions( 'socketfile=s' => \$socketfile,
	    'define=s'     => \%defines,
	    'outfile=s'    => \$outfile,
	    'help'         => \$help,
	    'man'          => \$man,
    );

pod2usage(
    {
	-exitval => 1,
	-verbose => 0,
    } ) if $help;
pod2usage(
    {
	-exitval => 1,
	-verbose => 2,
    } ) if $man;


###########################################################################
# process configuration file
if (! defined $socketfile) {
    die "No --socketfile specified. Stopped";
}

my $kn = KeyNanny->new( { SOCKETFILE => $socketfile } ) or die "Error: $@";

my $cmd = shift;

if ( !defined $cmd ) {
    usage;
    exit 0;
}

my $rc = 0;
if ( $cmd eq 'get' ) {
    while (my $arg = shift) {
	my $result = $kn->get($arg);
	
	if ($result->{STATUS} eq 'OK') {
	    print $result->{DATA};
	} else {
	    error_msg($result, 'Could not get key');
	    $rc = 1;
	}
    }
} elsif ( $cmd eq 'set' ) {
    my $arg = shift;
    my $value = shift;

    if (! defined $value) {
	local $/;
	$value = <STDIN>;
    }
    if (defined $value) {
	my $result = $kn->set($arg, $value);
	if ($result->{STATUS} ne 'OK') {
	    error_msg($result, 'Could not set key');
	    $rc = 1;
	}
    } else {
	die "No value specified. Stopped";
    }
} elsif ( $cmd eq 'link' ) {
    my $to = shift;
    my $from = shift;

    if ((defined $to) && (defined $from)) {
	my $result = $kn->link($to, $from);
	if ($result->{STATUS} ne 'OK') {
	    error_msg($result, 'Could not create link');
	    $rc = 1;
	}
    } else {
	die "Missing arguments. Stopped";
    }
} elsif ( $cmd eq 'list' ) {
    my $result = $kn->list();
    if ($result->{STATUS} eq 'OK') {
	print join("\n", @{$result->{KEYS}}) . "\n";
    } else {
	error_msg($result, "Could not get list of keys");
	$rc = 1;
    }
} elsif ( $cmd eq 'template' ) {
    my $infile = shift;

    if (! defined $outfile) {
	die "Missing --outfile option. Stopped";
    }

    my @variables;
    my $result = $kn->list();
    if ($result->{STATUS} eq 'OK') {
	@variables = @{$result->{KEYS}};
    } else {
	error_msg($result, "Could not get list of available entries from KeyNanny daemon");
	exit 1;
    }

    my %vars;
    foreach my $entry ( @variables ) {
	my $result = $kn->get($entry);
	if ($result->{STATUS} eq 'OK') {
	    $vars{$entry} = $result->{DATA};
	} else {
	    error_msg($result, "Could not get key $entry");
	    exit 1;
	}
    }
    # override values from keynanny by --defines on command line
    foreach my $entry (keys %defines) {
	$vars{$entry} = $defines{$entry};
    }

    if ( !process_template( $kn, $infile, $outfile, \%vars ) ) {
        $rc = 1;
    }
}
else {
    pod2usage("$0: invalid command $cmd");
}

exit $rc;

__END__

=head1 NAME

keynanny - Command line interface to the KeyNanny credential protection daemon

=head1 SYNOPSIS

keynanny [options] [command] [arguments]

  Options:
    --socketfile FILE     keynanny socket file
    --outfile FILE        template output file name
    --define VAR=VALUE    additional variables for template processing
    --help                brief help message
    --man                 full manual page

  Commands:
    get VAR               get variable VAR from keynanny
    set VAR VALUE         set variable VAR to VALUE
    list                  list all available variables
    template FILE         render template FILE, replacing keynanny variables
    link A B              create a symlink from B to A in an atomic operation


=head1 OPTIONS AND ARGUMENTS

=over 8

=item B<--socketfile FILE>

Connect to a KeyNanny daemon listening on server socket FILE.

=item B<--outfile FILE>

Write generated output to FILE.

Only applicable in template processing mode.

=item B<--define VAR=VALUE>

Define an additional variable VAR with value VALUE that will be available
within the template as [% VAR %]. --define may be specified repeatedly
to define multiple values.

Please note: in order to set a VALUE containing white space the correct
quoting syntax is --define 'VAR=foo bar' (not --define VAR='foo bar').

Only applicable in template processing mode.

=item B<--help>

Print a brief help message and exit.

=item B<--man>

Prints the manual page and exits.

=back

The following commands may be specified:

=over 8

=item B<get VAR>

Ask keynanny for the value of VAR and print it to STDOUT.

=item B<set VAR VALUE>

Set keynanny variable VAR to VALUE. STDIN is read if no VALUE is
specified.

=item B<link A B>

Create a symlink from B to A in an atomic operation. Files are relative
to the user's storage path (e.g. C</var/lib/keynanny/storage/app1/>).

=item B<list>

List all keys that are held by the keynanny daemon (one key per line).

=item B<template FILE>

Read FILE and process it as a Template::Toolkit template. The rendered
output is written to --outfile.
The template may reference variables via [% VAR %]. All variables protected 
by the keynanny daemon are available in the template.
It is possible to add additional tuples via the --define VAR=VALUE
syntax.
Please note that any values specified via --define overwrite those
read from keynanny if the same variable name is used.

=back
